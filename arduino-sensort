#include <WiFi.h>
#include <HTTPClient.h>
#include <ESPAsyncWebSrv.h>
#include <SPIFFS.h>
#include "DHT.h"

#define WIFI_SSID "Aminatou"
#define WIFI_PASSWORD "Aminatou"
#define DHTPIN 4    
#define DHTTYPE DHT11

const char* raspberryPiUrl = "http://192.168.0.105:8080";  // Assuming port 8000, adjust if needed

DHT dht(DHTPIN, DHTTYPE);
AsyncWebServer server(80);

void setup() {
  Serial.begin(115200);
  delay(10);

  Serial.println(F("DHTxx test!"));
  dht.begin();

  Serial.print("Connecting to network: ");
  Serial.println(WIFI_SSID);

  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  Serial.println("");
  Serial.println("WiFi is connected!");
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());

  if (!SPIFFS.begin(true)) {
    Serial.println("An error occurred while mounting SPIFFS");
    return;
  }

  server.on("/", HTTP_GET, [](AsyncWebServerRequest *request){
    request->send(SPIFFS, "/index.html", "text/html");
  });

  server.on("/sensor-data", HTTP_GET, [](AsyncWebServerRequest *request){
    float h = dht.readHumidity();
    float t = dht.readTemperature();
    String json = "{\"temperature\":" + String(t) + ",\"humidity\":" + String(h) + "}";
    request->send(200, "application/json", json);
  });

  server.begin();
}

void loop() {
  delay(5000);

  float h = dht.readHumidity();
  float t = dht.readTemperature();

  if (isnan(h) || isnan(t)) {
    Serial.println(F("Failed to read from DHT sensor!"));
    return;
  }

  Serial.print(F("Humidity: "));
  Serial.print(h);
  Serial.print(F("%  Temperature: "));
  Serial.print(t);
  Serial.println(F("Â°C"));

  // Send data to Raspberry Pi
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    http.begin(raspberryPiUrl);
    http.addHeader("Content-Type", "application/json");
    
    String jsonPayload = "{\"temperature\":" + String(t) + ",\"humidity\":" + String(h) + "}";
    int httpResponseCode = http.POST(jsonPayload);
    
    if (httpResponseCode > 0) {
      Serial.println("Data sent successfully to Raspberry Pi");
    } else {
      Serial.println("Error sending data to Raspberry Pi");
    }
    
    http.end();
  }
}
